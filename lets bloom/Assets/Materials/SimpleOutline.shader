Shader "Custom/SimpleOutline"
{
    Properties
    {
        _MainTex ("Texture", 2D) = "white" {}
        _OutlineColor ("Outline Color", Color) = (1,1,1,1)
        _Thickness ("Thickness", Range(0, 10)) = 1
    }
    SubShader
    {
        Tags { "Queue"="Transparent" "RenderType"="Transparent" }
        Blend SrcAlpha OneMinusSrcAlpha
        Cull Off
        ZWrite Off

        Pass
        {
            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag
            #include "UnityCG.cginc"

            struct appdata { float4 vertex : POSITION; float2 uv : TEXCOORD0; };
            struct v2f { float4 vertex : SV_POSITION; float2 uv : TEXCOORD0; };

            sampler2D _MainTex;
            float4 _MainTex_TexelSize;
            fixed4 _OutlineColor;
            float _Thickness;

            v2f vert (appdata v) {
                v2f o;
                o.vertex = UnityObjectToClipPos(v.vertex);
                o.uv = v.uv;
                return o;
            }

            fixed4 frag (v2f i) : SV_Target {
                fixed4 col = tex2D(_MainTex, i.uv);
                float2 size = _MainTex_TexelSize.xy * _Thickness;

                // Check neighbors (Up, Down, Left, Right)
                float alpha = tex2D(_MainTex, i.uv + float2(0, size.y)).a +
                              tex2D(_MainTex, i.uv - float2(0, size.y)).a +
                              tex2D(_MainTex, i.uv + float2(size.x, 0)).a +
                              tex2D(_MainTex, i.uv - float2(size.x, 0)).a;

                // If this pixel is empty but neighbors aren't, draw outline
                if (col.a < 0.1 && alpha > 0.1) return _OutlineColor;
                
                return col;
            }
            ENDCG
        }
    }
}